buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.3'
    }
}

allprojects {
  repositories {
      google()
      jcenter()
  }
}

apply plugin: 'com.android.library'

android {
    compileSdkVersion 29
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 5
        versionName "5.2"
        setProperty("archivesBaseName", "kaldi-android-$versionName")
        externalNativeBuild {
            cmake {
                arguments "-DCMAKE_VERBOSE_MAKEFILE=ON", "-DANDROID_ARM_NEON=TRUE", "-DCMAKE_CXX_FLAGS_RELEASE=-O3"
            }
        }
        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86_64', 'x86'
        }
    }
    sourceSets {
         main {
              java.srcDirs = ['src/main/java', 'build/generated-src']
         }
    }
    externalNativeBuild {
       cmake {
           path "CMakeLists.txt"
       }
    }
}

task swig {
    doLast {
        mkdir 'build/generated-src/java'
        mkdir 'build/generated-src/cpp'
        exec {
            commandLine 'swig',
            "-c++",
            "-java", "-package", "org.kaldi",
            "-outdir", "build/generated-src/java", "-o", "build/generated-src/cpp/vosk_wrap.cc",
            "../src/vosk.i"
        }
    }
}

task kaldi(type: Exec) {
    commandLine './build-kaldi.sh'
    environment ANDROID_SDK_HOME: android.getSdkDirectory()
}

preBuild.dependsOn kaldi, swig
